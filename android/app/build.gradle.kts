plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
}

android {
    namespace = "com.srgs.ems"
    compileSdk = 35

    defaultConfig {
        applicationId = "com.srgs.ems"
        minSdk = 26
        targetSdk = 35
        versionCode = 1
        versionName = "1.0.0"

        vectorDrawables { useSupportLibrary = true }
    }

    buildTypes {
        release {
            isMinifyEnabled   = true
            isShrinkResources = true
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }
        debug {
            // Keep debug fast — no minification
            isMinifyEnabled = false
        }
    }

    flavorDimensions += "client"

    productFlavors {
        create("vitadesk") {
            dimension = "client"
            applicationId = "com.srgs.vitadesk"
            resValue("string", "app_name", "VitaDesk")
            buildConfigField("String", "API_URL", "\"https://smsapi.srglanzsoftware.com/api\"")
            buildConfigField("String", "ENTITY_ID", "\"\"")
        }
        create("lakeshore") {
            dimension = "client"
            applicationId = "com.srgs.lakeshoreschool"
            resValue("string", "app_name", "Lakeshore School")
            buildConfigField("String", "API_URL", "\"https://smsapi.srglanzsoftware.com/api\"")
            buildConfigField("String", "ENTITY_ID", "\"69a3240d669273408df1969f\"")
        }
        create("revilation") {
            dimension = "client"
            applicationId = "com.revilation.app"
            resValue("string", "app_name", "Revilation Fitness")
            buildConfigField("String", "API_URL", "\"https://smsapi.srglanzsoftware.com/api\"")
            buildConfigField("String", "ENTITY_ID", "\"<entity_id>\"")
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions { jvmTarget = "17" }
    buildFeatures {
        compose = true
        buildConfig = true
    }
    composeOptions { kotlinCompilerExtensionVersion = "1.5.14" }
    packaging { resources { excludes += "/META-INF/{AL2.0,LGPL2.1}" } }
}

dependencies {
    implementation("androidx.core:core-ktx:1.13.1")
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:2.8.3")
    implementation("androidx.lifecycle:lifecycle-viewmodel-compose:2.8.3")
    implementation("androidx.activity:activity-compose:1.9.0")

    implementation(platform("androidx.compose:compose-bom:2024.06.00"))
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.ui:ui-graphics")
    implementation("androidx.compose.ui:ui-tooling-preview")
    implementation("androidx.compose.material3:material3")
    implementation("androidx.compose.material:material-icons-core")
    implementation("androidx.compose.ui:ui-text-google-fonts")

    implementation("androidx.navigation:navigation-compose:2.7.7")

    // Networking
    implementation("com.squareup.retrofit2:retrofit:2.11.0")
    implementation("com.squareup.retrofit2:converter-gson:2.11.0")
    implementation("com.squareup.okhttp3:logging-interceptor:4.12.0")

    // Image loading
    implementation("io.coil-kt:coil-compose:2.6.0")

    // Local storage
    implementation("androidx.datastore:datastore-preferences:1.1.1")

    testImplementation("junit:junit:4.13.2")
    androidTestImplementation(platform("androidx.compose:compose-bom:2024.06.00"))
    androidTestImplementation("androidx.compose.ui:ui-test-junit4")
    debugImplementation("androidx.compose.ui:ui-tooling")
    debugImplementation("androidx.compose.ui:ui-test-manifest")
}
